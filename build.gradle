apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = '1.8'

mainClassName = 'com.beta.Main'
version = '0.1.0'

repositories {
    mavenCentral()
}

configurations {
    capsule
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.7.1'
    compile 'io.dropwizard:dropwizard-db:0.7.1'
    compile 'io.dropwizard:dropwizard-jdbi:0.7.1'
    compile 'io.dropwizard:dropwizard-client:0.7.1'
    compile 'io.dropwizard:dropwizard-assets:0.7.1'
    compile 'io.dropwizard:dropwizard-testing:0.7.1'

    compile 'com.sun.jersey.contribs:jersey-multipart:1.18.1'

    compile 'com.netflix.feign:feign-core:6.1.2'
    compile 'com.netflix.feign:feign-jaxrs:6.1.2'
    compile 'com.netflix.feign:feign-jackson:6.1.2'

    runtime 'com.h2database:h2:1.4.178'

    capsule 'co.paralleluniverse:capsule:0.4.0'
    testCompile 'junit:junit:4.11'
}

task capsule(type: Jar, dependsOn: classes) {
    archiveName = "beta-web.jar"

    from jar // embed our application jar
    from { configurations.runtime } // embed dependencies
    from { sourceSets.main.resources }

    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' }
    // we just need the single Capsule class

    manifest {
        attributes(
                'Main-Class': 'Capsule',
                'Application-Class': mainClassName,
                'Application-Version': version,
                'Min-Java-Version': '1.8.0',
                'JVM-Args': run.jvmArgs.join(' '),
                'System-Properties': (run.systemProperties + ["dropwizard.config": '$CAPSULE_DIR/beta.yml']).collect { k, v -> "$k=$v" }.join(' '),
        )
    }
}

task stage(dependsOn: ['clean'])

                       run {
    systemProperty "dropwizard.config", "build/resources/main/beta.yml"
}